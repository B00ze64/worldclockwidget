buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:0.11.1'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.10.1'
    }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'android'

apply plugin: 'checkstyle'

android {
    compileSdkVersion 19
    buildToolsVersion "19.1.0"

    defaultConfig {
        versionCode 32
        versionName "1.6.0"

        minSdkVersion 10
        targetSdkVersion 19
    }

    if (project.hasProperty('signingKeyStoreFile')) {
        signingConfigs {
            release {
                storeFile     file(signingKeyStoreFile)
                storePassword signingKeyStorePassword
                keyAlias      signingKeyAlias
                keyPassword   signingKeyPassword
            }
        }
    }

    buildTypes {
        release {
            runProguard true
            proguardFile 'proguard-project.txt'
            if (project.hasProperty('signingKeyStoreFile')) {
                signingConfig signingConfigs.release
            }
        }
    }
}

///////////////////////////////////////////////////
// Dependencies

repositories {
    mavenCentral()
}

dependencies{
    compile 'com.actionbarsherlock:actionbarsherlock:4.4.0@aar'
    compile 'com.android.support:support-v4:19.1.0'
    compile 'com.google.code.gson:gson:2.2.4'
}


///////////////////////////////////////////////////
// Checkstyle

checkstyle {
    ignoreFailures = true
    toolVersion = '5.7'
    showViolations = false
    configFile = rootProject.file('config/checkstyle/checkstyle.xml')
}

task checkstyleDebug(type: Checkstyle, dependsOn: 'compileDebugJava') {
    source = fileTree('src/main/java/')
    classpath = files('build/intermediates/classes/debug')
}
check.dependsOn checkstyleDebug
